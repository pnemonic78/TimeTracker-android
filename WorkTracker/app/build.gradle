apply from: '../constants.gradle'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def versionMajor = Integer.parseInt(project.APP_VERSION_MAJOR)
def versionMinor = Integer.parseInt(project.APP_VERSION_MINOR)

android {
    compileSdkVersion project.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.tikalk.worktracker"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode versionMajor * 1000 + versionMinor
        versionName versionMajor + '.' + versionMinor.toString().padLeft(2, '0')
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary true
    }

    signingConfigs {
        release {
            storeFile file("../release.keystore")
            storePassword project.STORE_PASSWORD_RELEASE
            keyAlias "release"
            keyPassword project.KEY_PASSWORD_RELEASE
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            // disabled until fix proguard issues: minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        androidTest {
            java.srcDirs += 'src/androidTest/kotlin'
        }
        main {
            java.srcDirs += 'src/main/kotlin'
        }
        test {
            java.srcDirs += 'src/test/kotlin'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        freeCompilerArgs += ["-Xallow-result-return-type"]
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/INDEX.LIST'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"

    // Google Support
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.security:security-crypto:1.1.0-alpha01'

    // Testing
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation "junit:junit:$junitVersion"

    // Database
    implementation "androidx.room:room-common:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Rx
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid2Version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin2Version"

    // Web
    implementation "com.squareup.retrofit2:retrofit:$retrofit2Version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit2Version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2Version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit2Version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttpVersion"
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.jsoup:jsoup:1.12.1'

    // Logging
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Export
    implementation('com.opencsv:opencsv:5.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation 'org.jetbrains.kotlinx:kotlinx-html-jvm:0.6.10'
    /// ODF
    implementation 'org.odftoolkit:odfdom-java:0.8.7'
    implementation 'com.fasterxml.woodstox:woodstox-core:6.2.0'
    implementation 'org.w3c:dom:2.3.0-jaxb-1.0.6'

    // Koin
    implementation 'org.koin:koin-android:2.0.1'
    implementation 'org.koin:koin-androidx-scope:2.0.1'
    //implementation 'org.koin:koin-androidx-viewmodel:2.0.1'
}
